plugins {
    id 'build-dashboard'
    id 'checkstyle'
    id 'findbugs'
    id 'jacoco'
    id 'java'
}

group = 'hm.binkley'
version = '0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.18'
    testCompileOnly 'org.projectlombok:lombok:1.16.18'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile 'org.mockito:mockito-core:2.1.0'
}

tasks.withType(AbstractCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.with {
        compilerArgs += ['-Werror', '-Xlint:all', '-Xlint:-processing']
        encoding = 'UTF-8'
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(FindBugs) {
    reports {
        html.enabled true
        xml.enabled false
    }
}

clean {
    delete 'out'
}

jacoco {
    toolVersion = '0.7.10-SNAPSHOT'
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 1.00
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}
